ruleConclusion =
    "Allow"
  | "AllowImmediately"
  | "Deny"
  | "DenyImmediately"
  ;

ruleConclusionE =
  "(" , "Conclusion" , ruleConclusion , ")"
  ;

matchSubjectE =
    "True"
  | "Talse"
  | "(" , "WithAllRolesFrom" , name* , ")"
  | "(" , "WithAnyRolesFrom" , name* , ")"
  | "(" , "Or" , matchSubjectE , matchSubjectE , ")"
  | "(" , "And" , matchSubjectE , matchSubjectE , ")"
  ;

matchSubject =
  "(" , "MatchSubject" , matchSubjectE , ")" ;

attribute =
  "(" , "Attribute" , name , name , ")" ;

matchObjectE =
    "True"
  | "False"
  | "(" , "WithType" , name , ")"
  | "(" , "WithAllAttributesFrom" , attribute* , ")"
  | "(" , "WithAnyAttributesFrom" , attribute* , ")"
  | "(" , "Or" , matchObjectE , matchObjectE , ")"
  | "(" , "And" , matchObjectE , matchObjectE , ")"
  ;

matchObject =
  "(" , "MatchObject" , matchObjectE , ")" ;

matchActionE =
    "True"
  | "False"
  | "(" , "WithName" , name , ")"
  | "(" , "Or" , matchActionE , matchActionE , ")"
  | "(" , "And" , matchActionE , matchActionE , ")"
  ;

matchAction =
  "(" , "MatchAction" , matchActionE , ")" ;

ruleName =
  "(" , "Name" , name , ")" ;

ruleDescription =
  "(" , "Description" , quoted , ")" ;

ruleElement =
    ruleName?
  | ruleDescription?
  | ruleConclusionE
  | matchSubject
  | matchObject
  | matchAction
  ;

rule =
  "(" , "Rule" , ruleElement* , ")" ;

quoted =
  <Any s-expression quoted string> ;

name =
  <A valid Lanark dotted name> ;

digit =
  "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

digit-and-zero =
  digit | "0" ;

number =
  digit , digit-and-zero* ;

medrina =
  "(" , "Medrina" , number , number , ")" ;

policy =
  medrina , rule* ;
